---
layout: post
title: "Sistema de Monitoreo Ambiental con Arduino, Sensor DHT11 y Pantalla TFT LCD"
date: 2025-05-13 12:00:00 
categories: arduino proyectos
excerpt: "En este emocionante proyecto de electrónica, hemos creado un sistema de monitoreo ambiental utilizando una placa Arduino, un sensor de temperatura y humedad DHT11 y una pantalla TFT LCD. Nuestra solución permite capturar datos ambientales esenciales en tiempo real y presentarlos de manera visualmente atractiva en la pantalla, brindando una visión rápida y accesible de las condiciones del entorno."
---

En este emocionante proyecto de electrónica, hemos creado un sistema de monitoreo ambiental utilizando una placa Arduino, un sensor de temperatura y humedad DHT11 y una pantalla TFT LCD. Nuestra solución permite capturar datos ambientales esenciales en tiempo real y presentarlos de manera visualmente atractiva en la pantalla, brindando una visión rápida y accesible de las condiciones del entorno.

## Introducción

Este proyecto combina la potencia del Arduino, la precisión del sensor DHT11 y la capacidad de visualización de una pantalla TFT LCD para crear un sistema completo de monitoreo ambiental. El sensor DHT11 es capaz de medir tanto la temperatura como la humedad relativa del entorno, y el Arduino procesa estos datos para mostrarlos de manera clara en la pantalla.

La pantalla TFT LCD, con su vibrante visualización a color, permite presentar de forma gráfica los datos capturados por el sensor DHT11. Mostramos la temperatura actual y el nivel de humedad en tiempo real, lo que facilita la comprensión de las condiciones ambientales de un solo vistazo. Además, hemos diseñado una interfaz intuitiva que permite a los usuarios ver la relación entre las mediciones y el índice de calor o sensación térmica.

Este proyecto no solo es una excelente manera de aprender sobre electrónica y programación, sino que también tiene aplicaciones prácticas en entornos donde el control de la temperatura y la humedad es esencial, como invernaderos, cuartos de almacenamiento y más. Nuestra documentación detallada incluye el esquema de conexiones, el código fuente y explicaciones paso a paso para que puedas recrear este sistema de monitoreo ambiental por ti mismo.

¡Explora cómo la tecnología puede ayudarnos a entender mejor nuestro entorno con este emocionante proyecto de Arduino, sensor DHT11 y pantalla TFT LCD!

## Lista de materiales

Los materiales necesarios para realizar el montaje del proyecto son:

- 1x Arduino UNO Rev.3
- 1x Pantalla TFT LCD táctil 2,4’’ (o "Shield TFT LCD Touch 2,4’’")
- 1x Módulo DHT11
- Cables de conexión (o "cables para conexiones")

## Diagrama de conexiones

Dado que el shield TFT tiene exactamente el mismo tamaño que un Arduino, para usarlo simplemente colócalo sobre tu Arduino. ¡Sin necesidad de cableado adicional ni soldadura!

![Shield TFT LCD Touch 2,4''](/assets/images/projects/P02-01.png)

Sin embargo, como el shield cubre todos los pines de la placa Arduino, es necesario hacer un pequeño ajuste: debemos soldar cables a los pines "5V", "GND" y "1" para proporcionar alimentación al sensor DHT11 y una señal con los datos de la lectura.

![Diagrama de conexiones](/assets/images/projects/P02-02.png)

## Código

El código para este proyecto es el siguiente:

```cpp
/*
 * CartroTech  (cartrotech.github.io)
 * 
 * Projecte nº: P002
 * Descripcio: Llegir les dades d'un sensor DHT11 i presentarles en una pantalla TFT LCD
 * 
 * 09.08.2023
 */

#include <DHT.h>
#include <DHT_U.h>
#include <Adafruit_GFX.h>
#include <Adafruit_GrayOLED.h>
#include <Adafruit_SPITFT.h>
#include <Adafruit_SPITFT_Macros.h>
#include <gfxfont.h>
#include  <MCUFRIEND_kbv.h>

#define DHTPIN 1      //Pin digital connectat al sensor DHT
#define DHTTYPE DHT11 //Tipus de sensor

#define DEG2RAD 0.0174532925

// Colors
#define BLACK   0x0000
#define BLUE    0x001F
#define RED     0xF800
#define GREEN   0x07E0
#define CYAN    0x07FF
#define MAGENTA 0xF81F
#define YELLOW  0xFFE0
#define WHITE   0xFFFF

MCUFRIEND_kbv tft;         //Declarar l’objecte tft
DHT dht(DHTPIN, DHTTYPE);  //Declarar l'objecte DHT amb el parametres PIN i TYPE

float HR;  // Humitat relativa
float TC;  // Temperatura
float IC;  // Index de calor
float antHR = 0;
float antTC = 0;
float antIC = 0; 

// Paràmetres del diseny de pantalla (dimensions 314x234)
int xcT = 314*1/4; //Coord. horitzontal del centre de l'indicador de Temperatura
int ycT = 234*1/2; //Coord. vertical del centre de l'indicador de Temperatura
int xcH = 314*3/4; //Coord. horitzontal del centre de l'indicador de Humitat
int ycH = 234*1/2; //Coord. vertical del centre de l'indicador de Humitat
int Radi = 60; //Radi de l'indicador
int arcAmp = 270; //Amplitut de l'arc de l'indicador
int angIni = -45; //Angle inicial de l'arc de l'indicador
int colRgb565 = 0;
int colorTC = CYAN;
int colorHR = RED;
int colorIC = MAGENTA;
int limitSuperiorTemp = 40;
int limitSuperiorHumitat = 100;

void setup() 
{

  // DHT setup
  dht.begin();              //Inicialitza l'objecte DHT

  // TFT setup
  uint16_t ID = tft.readID();
  tft.begin(ID);
  tft.fillScreen(BLACK);
  tft.setRotation(1);       //Estableix pantalla apaisada
  tft.setTextSize(2);       //Tamany del text
  tft.setTextColor(WHITE);  //Color del text

}

void loop() 
{
  // Mesures del sensor DHT11

  delay(2000);  //Espera 2 seg entre mesures

  HR = dht.readHumidity();               //Llig l'humitat
  TC = dht.readTemperature();            //Llig la temperatura en Celsius
  if (isnan(HR) || isnan(TC))             //Comprova que no hi ha error
    {
      tft.setTextSize(2);
      tft.setCursor(20, ycT);
      tft.setTextSize(WHITE);
      tft.println("Fallo llegint sensor DHT!");
      delay (1000);
      tft.setTextSize(2);
      tft.setCursor(20, ycT);
      tft.setTextSize(BLACK);
      tft.println("Fallo llegint sensor DHT!");
      dibuixaPantalla(colorTC, colorHR);
      return;
    }
  IC = dht.computeHeatIndex(TC, HR, false);        //Calcula index de calor en Celsius


  colorTC = colorTemp(TC);
  colorHR = colorHumi(HR);
  //colorIC = colorIndex(IC);
  
  dibuixaPantalla(colorTC, colorHR);

  //Borra l'agulla anterior
  dibuixaAgullaHR(antHR, BLACK);
  dibuixaAgullaTC(antTC, BLACK);

  //Diuixa l'agulla nova
  dibuixaAgullaHR(HR, colorHR);
  dibuixaAgullaTC(TC, colorTC);

  //Converteix els valors actuals a antics
  antHR = HR;
  antTC = TC;
}

// --------------------------------- Funcions ------------------------

//Defineix el color de l'escala de temperatura segons el seu valor
int colorTemp(float Temp)
{
  uint16_t Temp_Rgb565 = 0;

  int R = int(0 + ((Temp + 10) * 5.1));
  int G = int(255 - ((Temp + 10) * 5.1));
  int B = int(255 - ((Temp + 10) * 5.1));


  Temp_Rgb565 = (R & 0b11111000) <<8;
  Temp_Rgb565 = Temp_Rgb565 + ((G & 0b11111100 ) <<3);
  Temp_Rgb565 = Temp_Rgb565 + ((B) >> 3);

  return Temp_Rgb565;
}

//Defineix el color de l'escala de humitat segons el seu valor
int colorHumi(float Humi)
{
  uint16_t Humi_Rgb565 = 0;

  int R = int(255 - (Humi * 255 / 100));
  int G = int(255 - (Humi * 255 / 100));
  int B = int(Humi * 255 / 100);


  Humi_Rgb565 = (R & 0b11111000) <<8;
  Humi_Rgb565 = Humi_Rgb565 + ((G & 0b11111100 ) <<3);
  Humi_Rgb565 = Humi_Rgb565 + ((B) >> 3);

  return Humi_Rgb565;
}

// Traça l'agulla indicadora de temperatura
void dibuixaAgullaTC(float temperatura, uint16_t color)
{
  float angAgulla = angIni + ((limitSuperiorTemp - temperatura) * 5.4);

  float xAgulla = xcT + ((Radi - 10) * cos(angAgulla * DEG2RAD));
  float yAgulla = ycT - ((Radi - 10) * sin(angAgulla * DEG2RAD));
  tft.drawLine(xcT, ycT, int(xAgulla), int(yAgulla), color);
  int posxTC = xcT - 20;
  int posyTC = 130;
  tft.setTextSize(2);
  tft.setCursor(posxTC, posyTC);
  tft.setTextColor(BLACK);
  tft.print(antTC, 1);
  tft.setCursor(posxTC, posyTC);
  tft.setTextColor(color);
  tft.print(TC, 1);
}

// Traça l'agulla indicadora de humitat
void dibuixaAgullaHR(float humitat, uint16_t color)
{
  float angAgullaH = angIni + ((limitSuperiorHumitat - humitat) * 2.7);
  float xAgullaH = xcH + ((Radi - 10) * cos(angAgullaH * DEG2RAD));
  float yAgullaH = ycH - ((Radi - 10) * sin(angAgullaH * DEG2RAD));
  tft.drawLine(xcH, ycH, int(xAgullaH), int(yAgullaH), color);
  int posxHR = xcH - 10;
  int posyHR = 130;
  tft.setTextSize(2);
  tft.setCursor(posxHR, posyHR);
  tft.setTextColor(BLACK);
  tft.print(int(antHR));
  tft.setCursor(posxHR, posyHR);
  tft.setTextColor(color);
  tft.print(int(HR));
}

// Presentacio en pantalla TFT LCD
void dibuixaPantalla(int colorTemp, int colorHumi)  
{
  for (int r=0; r<4; r++)
  {
    dibuixaArc(xcT, ycT, Radi-r, arcAmp, angIni, colorTemp); // Indicador temp: coord. centre (x, y), radi, amplitut de l'arc, angle inicial i color
    dibuixaArc(xcH, ycH, Radi-r, arcAmp, angIni, colorHumi);  // Indicador humi: coord. centre (x, y), radi, amplitut de l'arc, angle inicial i color
  }
  dibuixaEscala(xcT, ycT, Radi, colorTemp, 1); // Dibuixa l'escala per a temperatura
  dibuixaEscala(xcH, ycH, Radi, colorHumi, 2); // Dibuixa l'escala per a humitat
  dibuixaNoms();
}

// Dibuixa un arc de radi, amplitut i angle inicial determinats
void dibuixaArc(int xc, int yc, int R, int amplitut, int angleInicial, uint16_t Rgb565) 
{

  float x0 = xc + (R * cos(angleInicial*DEG2RAD)); // Calcula el valor inical de la coord. x del primer punt de l'arc
  float y0 = yc - (R * sin(angleInicial*DEG2RAD)); // Calcula el valor inical de la coord. y del primer punt de l'arc
  
  for (int i = 0; i < amplitut; i++)  //Calcul de la resta de punts de l'arc
  {
    float x1 = xc + (R * cos((i+angleInicial)*DEG2RAD));
    float y1 = yc - (R * sin((i+angleInicial)*DEG2RAD));
    tft.drawLine(int(x0), int(y0), int(x1), int(y1), Rgb565); //Dibuixa una línia entre els dos punts de l'arc
    x0 = x1; // Guarda la nova coordenada x com antiga per continuar el bucle
    y0 = y1; // Guarda la nova coordenada x com antiga per continuar el bucle
  }
}

//Dibuixa travesses de la escala
void dibuixaEscala(int xc, int yc, int R, uint16_t Rgb565, int tipus)
{
  float angEscala = arcAmp/10;

  for(int i=0; i<=10; i++) //Travesses indicador
  {
    float x1 = xc + (R * cos((angIni+(i*angEscala))*DEG2RAD));
    float y1 = yc - (R * sin((angIni+(i*angEscala))*DEG2RAD));

    float x2 = xc + ((R+7) * cos((angIni+(i*angEscala))*DEG2RAD));
    float y2 = yc - ((R+7) * sin((angIni+(i*angEscala))*DEG2RAD));

    tft.drawLine(int(x1), int(y1), int(x2), int(y2), Rgb565);

    if (i == 0 || i == 3 || i == 5 || i == 7 || i == 10)
    {
      float x3 = xc + ((R+20) * cos((angIni+(i*angEscala))*DEG2RAD));
      float y3 = yc - ((R+20) * sin((angIni+(i*angEscala))*DEG2RAD));

      if (tipus == 1)
      {
        tft.setCursor(x3-4, y3);
        tft.setTextSize(1);
        tft.setTextColor(Rgb565);
        tft.print(limitSuperiorTemp - (i*5));
        
      }
      else if (tipus == 2)
      {
        tft.setCursor(x3-4, y3);
        tft.setTextSize(1);
        tft.setTextColor(Rgb565);
        tft.print(limitSuperiorHumitat - (i*10));
      }
      else
      {
        tft.setCursor(x3-4, y3);
        tft.setTextSize(1);
        tft.setTextColor(Rgb565);
        tft.print("-");
      }

    }
  }
}


void dibuixaNoms()
{
  tft.setTextSize(2);
  tft.setCursor(40, 210);
  tft.setTextColor(colorTC);
  tft.print("Temp.");
  tft.write(0xF7);
  tft.print("C");

  tft.setCursor(205, 210);
  tft.setTextColor(colorHR);
  tft.print("Humi.%");
}

```

## Imágenes

La ejecución del código en el Arduino UNO Rev.3 con el shield TFT LCD Touch 2,4'' y el sensor DHT11 da como resultado las siguientes imágenes.

Observa que el color de los indicadores de temperatura y humedad cambia según el valor de la medida.

![Pantalla TFT LCD](/assets/images/projects/P02-03.jpg)

![Pantalla TFT LCD](/assets/images/projects/P02-04.jpg)

## Recursos y Referències

- Arduino UNO R3. Arduino. (20 de setembre de 2023). [Arduino UNO R3](https://docs.arduino.cc/hardware/uno-rev3)
- Shield TFT LCD Touch 2,4’’. Adafruit. (30 de novembre de 2012). [TFT Touch Shield.](https://learn.adafruit.com/2-8-tft-touch-shield/overview)
- Módulo DHT11. ArduinoModules. (03 de desembre de 2021). [KY-015 Temperature and Humidity Sensor Module.](https://arduinomodules.info/ky-015-temperature-humidity-sensor-module/)
- Hoja de características del módulo DHT11. [DHT11-DataSheet](https://mega.nz/file/nPIV2SAZ#VumiP2nCPm_octIgDTG9bTDux0agDAwZdjltBw8eYI4)
